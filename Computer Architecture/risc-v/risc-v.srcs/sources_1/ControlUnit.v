`timescale 1ns / 1ps
//////////////////////////////////////////////////////////////////////////////////
// Company: USTC ESLAB (Embeded System Lab)
// Engineer: Haojun Xia
// Create Date: 2019/02/08
// Design Name: RISCV-Pipline CPU
// Module Name: ControlUnit
// Target Devices: Nexys4
// Tool Versions: Vivado 2017.4.1
// Description: RISC-V Instruction Decoder
//////////////////////////////////////////////////////////////////////////////////
`include "Parameters.v"   
module ControlUnit(
    input wire [6:0] Op,
    input wire [2:0] Fn3,
    input wire [6:0] Fn7,
    output wire JalD,
    output wire JalrD,
    output reg [2:0] RegWriteD,
    output wire MemToRegD,
    output reg [3:0] MemWriteD,
    output wire LoadNpcD,
    output reg [1:0] RegReadD,
    output reg [2:0] BranchTypeD,
    output reg [3:0] AluContrlD,
    output wire [1:0] AluSrc2D,
    output wire AluSrc1D,
    output reg [2:0] ImmType        
    );
    
    assign JalD = (Op == 7'b1101111) ? 1'b1 : 1'b0;             //Jal
    assign JalrD = (Op == 7'b1100111) ? 1'b1 : 1'b0;            //Jalr
    assign MemToRegD = (Op == 7'b0000011) ? 1'b1 : 1'b0;        //Load:lb,lbu,lh,lhu,lw
    assign LoadNpcD = JalD | JalrD;
    assign AluSrc1D = (Op == 7'b0010111) ? 1'b1 : 1'b0;
                    //AluSrc1D = 1'b1: AUIPC
    assign AluSrc2D = (Op == 7'b0010011 &&(Fn3 == 3'b001 || Fn3 == 3'b101)) ? 2'b01     //AluSrc2D = 2'b01: SLLI,SRLI,SRAI
                        : ((Op == 7'b0110011 || Op == 7'b1100011) ? 2'b00 : 2'b10);     //AluSrc2D = 2'b00: R-type,Branch
                       
    always @ (*)
    begin
        case(Op)
            7'b0010011:begin 
                            RegWriteD <= `LW; MemWriteD <= 4'b0000; RegReadD <= 2'b10; BranchTypeD <= `NOBRANCH; ImmType <= `ITYPE;
                            case(Fn3)
                                3'b000: AluContrlD <= `ADD;     //ADDI
                                3'b010: AluContrlD <= `SLT;     //SLTI
                                3'b011: AluContrlD <= `SLTU;    //SLTIU
                                3'b111: AluContrlD <= `AND;     //ANDI
                                3'b110: AluContrlD <= `OR;      //ORI
                                3'b100: AluContrlD <= `XOR;     //XORI
                                3'b001: AluContrlD <= `SLL;     //SLLI
                                3'b101: AluContrlD <= (Fn7 == 7'b0000000) ? `SRL : `SRA;        //SRLI,SRAI
                                default: AluContrlD <= `ADD;
                            endcase
                        end           
            7'b0110111: begin RegWriteD <= `LW; MemWriteD <= 4'b0000; RegReadD <= 2'b00;        //LUI
                                BranchTypeD <= `NOBRANCH; AluContrlD <= `LUI; ImmType <= `UTYPE; end
            7'b0010111: begin RegWriteD <= `LW; MemWriteD <= 4'b0000; RegReadD <= 2'b00;        //AUIPC
                                BranchTypeD <= `NOBRANCH; AluContrlD <= `ADD; ImmType <= `UTYPE; end
            7'b0110011: begin 
                            RegWriteD <= `LW; MemWriteD <= 4'b0000; RegReadD <= 2'b11;        //R-type
                            BranchTypeD <= `NOBRANCH; ImmType <= `RTYPE;
                            case(Fn3)
                                3'b000: AluContrlD <= (Fn7 == 7'b0000000) ? `ADD : `SUB;
                                3'b001: AluContrlD <= `SLL;
                                3'b010: AluContrlD <= `SLT;
                                3'b011: AluContrlD <= `SLTU;
                                3'b100: AluContrlD <= `XOR;
                                3'b101: AluContrlD <= (Fn7 == 7'b0000000) ? `SRL : `SRA;
                                3'b110: AluContrlD <= `OR;
                                3'b111: AluContrlD <= `AND;
                                default: AluContrlD <= `ADD;
                            endcase
                        end
            7'b1101111: begin RegWriteD <= `LW; MemWriteD <= 4'b0000; RegReadD <= 2'b00;        //Jal
                                BranchTypeD <= `NOBRANCH; AluContrlD <= `ADD; ImmType <= `JTYPE; end
            7'b1100111: begin RegWriteD <= `LW; MemWriteD <= 4'b0000; RegReadD <= 2'b10;        //Jalr
                                BranchTypeD <= `NOBRANCH; AluContrlD <= `ADD; ImmType <= `ITYPE; end
                        //ÕâÀïÓÖ·¸ÁË´íÎó£¬Ò»¿ªÊ¼ÒÔÎªRegRead = 2'b00
            7'b1100011: begin 
                            RegWriteD <= `NOREGWRITE; MemWriteD <= 4'b0000; RegReadD <= 2'b11;  //Branch
                            ImmType <= `BTYPE; AluContrlD <= `ADD;          //AluContrlÎªÎÞ¹ØÏî
                            case(Fn3)
                                3'b000: BranchTypeD <= `BEQ;
                                3'b001: BranchTypeD <= `BNE;
                                3'b100: BranchTypeD <= `BLT;
                                3'b101: BranchTypeD <= `BGE;
                                3'b110: BranchTypeD <= `BLTU;
                                3'b111: BranchTypeD <= `BGEU;
                                default: BranchTypeD <= `BEQ;
                            endcase
                        end
            7'b0000011: begin                                           //Load
                            MemWriteD <= 4'b0000; RegReadD <= 2'b10; BranchTypeD <= `NOBRANCH; 
                            AluContrlD <= `ADD; ImmType <= `ITYPE;
                            case(Fn3)
                                3'b000: RegWriteD <= `LB;
                                3'b001: RegWriteD <= `LH;
                                3'b010: RegWriteD <= `LW;
                                3'b100: RegWriteD <= `LBU;
                                3'b101: RegWriteD <= `LHU;
                                default: RegWriteD <= `LW;
                            endcase
                        end
            7'b0100011: begin                                           //Store
                            RegWriteD <= `NOREGWRITE; RegReadD <= 2'b11; BranchTypeD <= `NOBRANCH;
                            AluContrlD <= `ADD; ImmType <= `STYPE;
                            case(Fn3)
                                3'b000: MemWriteD <= 4'b0001;
                                3'b001: MemWriteD <= 4'b0011;
                                3'b010: MemWriteD <= 4'b1111;
                                default: MemWriteD <= 4'b0000;
                            endcase
                        end
            default: begin RegWriteD <= `NOREGWRITE; MemWriteD <= 4'b0000; RegReadD <= 2'b00;  //Branch
                        BranchTypeD <= `NOBRANCH; ImmType <= `ITYPE; AluContrlD <= `ADD; end
                        //²È¿ÓÖ®Ò»£¬Ò»¿ªÊ¼BranchTypeDµÄdefaultÖµÉèÎªÁËBEQ£¬µ¼ÖÂBranchMakingDecisionÄ£¿éµÄBranchÐÅºÅÒ»Ö±ÎªÕæ¡£
        endcase     
    end
    
endmodule

//åŠŸèƒ½è¯´æ˜Ž
    //ControlUnit       æ˜¯æœ¬CPUçš„æŒ‡ä»¤è¯‘ç å™¨ï¼Œç»„åˆé?»è¾‘ç”µè·¯
//è¾“å…¥
    // Op               æ˜¯æŒ‡ä»¤çš„æ“ä½œç éƒ¨åˆ?
    // Fn3              æ˜¯æŒ‡ä»¤çš„func3éƒ¨åˆ†
    // Fn7              æ˜¯æŒ‡ä»¤çš„func7éƒ¨åˆ†
//è¾“å‡º
    // JalD==1          è¡¨ç¤ºJalæŒ‡ä»¤åˆ°è¾¾IDè¯‘ç é˜¶æ®µ
    // JalrD==1         è¡¨ç¤ºJalræŒ‡ä»¤åˆ°è¾¾IDè¯‘ç é˜¶æ®µ
    // RegWriteD        è¡¨ç¤ºIDé˜¶æ®µçš„æŒ‡ä»¤å¯¹åº”çš„ å¯„å­˜å™¨å†™å…¥æ¨¡å¼? ï¼Œæ‰€æœ‰æ¨¡å¼å®šä¹‰åœ¨Parameters.vä¸?
    // MemToRegD==1     è¡¨ç¤ºIDé˜¶æ®µçš„æŒ‡ä»¤éœ€è¦å°†data memoryè¯»å–çš„å?¼å†™å…¥å¯„å­˜å™¨,
    // MemWriteD        å…?4bitï¼Œé‡‡ç”¨ç‹¬çƒ­ç æ ¼å¼ï¼Œå¯¹äºŽdata memoryçš?32bitå­—æŒ‰byteè¿›è¡Œå†™å…¥,MemWriteD=0001è¡¨ç¤ºåªå†™å…¥æœ€ä½?1ä¸ªbyteï¼Œå’Œxilinx bramçš„æŽ¥å£ç±»ä¼?
    // LoadNpcD==1      è¡¨ç¤ºå°†NextPCè¾“å‡ºåˆ°ResultM
    // RegReadD[1]==1   è¡¨ç¤ºA1å¯¹åº”çš„å¯„å­˜å™¨å€¼è¢«ä½¿ç”¨åˆ°äº†ï¼ŒRegReadD[0]==1è¡¨ç¤ºA2å¯¹åº”çš„å¯„å­˜å™¨å€¼è¢«ä½¿ç”¨åˆ°äº†ï¼Œç”¨äºŽforwardçš„å¤„ç?
    // BranchTypeD      è¡¨ç¤ºä¸åŒçš„åˆ†æ”¯ç±»åž‹ï¼Œæ‰?æœ‰ç±»åž‹å®šä¹‰åœ¨Parameters.vä¸?
    // AluContrlD       è¡¨ç¤ºä¸åŒçš„ALUè®¡ç®—åŠŸèƒ½ï¼Œæ‰€æœ‰ç±»åž‹å®šä¹‰åœ¨Parameters.vä¸?
    // AluSrc2D         è¡¨ç¤ºAluè¾“å…¥æº?2çš„é?‰æ‹©
    // AluSrc1D         è¡¨ç¤ºAluè¾“å…¥æº?1çš„é?‰æ‹©
    // ImmType          è¡¨ç¤ºæŒ‡ä»¤çš„ç«‹å³æ•°æ ¼å¼ï¼Œæ‰€æœ‰ç±»åž‹å®šä¹‰åœ¨Parameters.vä¸?   
//å®žéªŒè¦æ±‚  
    //å®žçŽ°ControlUnitæ¨¡å—   